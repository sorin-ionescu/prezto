#
# A simple theme that only shows relevant information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/aipDQ.png
#

function prompt_sorin_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_sorin_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  add-zsh-hook precmd prompt_sorin_precmd

  zstyle ':omz:editor' completing '%B%F{red}...%f%b'
  zstyle ':omz:prompt:vi' insert ''
  zstyle ':omz:prompt:vi' command ' %F{yellow}❮%f%B%F{red}❮%f%b%F{red}❮%f'
  zstyle ':omz:module:git:prompt' action ':%%B%F{yellow}%s%f%%b'
  zstyle ':omz:module:git:prompt' added ' %%B%F{green}✚%f%%b'
  zstyle ':omz:module:git:prompt' ahead ' %%B%F{yellow}⬆%f%%b'
  zstyle ':omz:module:git:prompt' behind ' %%B%F{yellow}⬇%f%%b'
  zstyle ':omz:module:git:prompt' branch ':%F{red}%b%f'
  zstyle ':omz:module:git:prompt' deleted ' %%B%F{red}✖%f%%b'
  zstyle ':omz:module:git:prompt' modified ' %%B%F{blue}✱%f%%b'
  zstyle ':omz:module:git:prompt' renamed ' %%B%F{magenta}➜%f%%b'
  zstyle ':omz:module:git:prompt' commit ':%F{green}%.7c%f'
  zstyle ':omz:module:git:prompt' stashed ' %%B%F{cyan}✭%f%%b'
  zstyle ':omz:module:git:prompt' unmerged ' %%B%F{yellow}═%f%%b'
  zstyle ':omz:module:git:prompt' untracked ' %%B%F{white}◼%f%%b'
  zstyle ':omz:module:git:prompt' prompt ' %F{blue}git%f$(coalesce "%b" "%c")%s'
  zstyle ':omz:module:git:prompt' rprompt '%A%B%S%a%d%m%r%U%u'

  PROMPT='%F{cyan}%1~%f${(e)git_prompt_info} %(!.%B%F{red}#%f%b.%B%F{green}❯%f%b) '
  RPROMPT='${vi_prompt_info}%(?:: %F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}${git_rprompt_info}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_sorin_setup "$@"

