#
# Simple, colourful and one-line theme.
# Based on "sorin" theme.
#
# Authors:
#   Paul Miller <paul@paulmillr.com>
#
# Screenshots:
#   
#   http://cl.ly/0Y3N0O2C3u1A1K3D3A00/screen.png
#

function prompt_paulmillr_precmd() {
  if [[ -n "$__PROMPT_PAULMILLR_VCS_UPDATE" ]] ; then
    # Check for untracked files or updated submodules since vcs_info doesn't.
    if [[ ! -z $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
      __PROMPT_PAULMILLR_VCS_UPDATE=1
      fmt_branch="(${__PROMPT_PAULMILLR_COLORS[1]}%b%f%u%c${__PROMPT_PAULMILLR_COLORS[4]}!%f)"
    else
      fmt_branch="(${__PROMPT_PAULMILLR_COLORS[1]}%b%f%u%c)"
    fi
    zstyle ':vcs_info:*:prompt:*' formats "${fmt_branch}"

    vcs_info 'prompt'
    __PROMPT_PAULMILLR_VCS_UPDATE=''
  fi
}

function prompt_paulmillr_preexec() {
  case "$(history $HISTCMD)" in
    (*git*)
      __PROMPT_PAULMILLR_VCS_UPDATE=1
    ;;
    (*svn*)
      __PROMPT_PAULMILLR_VCS_UPDATE=1
    ;;
  esac
}

function prompt_paulmillr_chpwd() {
  __PROMPT_PAULMILLR_VCS_UPDATE=1
}

function prompt_paulmillr_setup() {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  add-zsh-hook precmd prompt_paulmillr_precmd
  add-zsh-hook preexec prompt_paulmillr_preexec
  add-zsh-hook chpwd prompt_paulmillr_chpwd

  __PROMPT_PAULMILLR_VCS_UPDATE=1

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_PAULMILLR_COLORS=(
      "%F{81}"  # turquoise
      "%F{166}" # orange
      "%F{135}" # purple
      "%F{161}" # hotpink
      "%F{118}" # limegreen
    )
  else
    __PROMPT_PAULMILLR_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Enable VCS systems you use.
  zstyle ':vcs_info:*' enable git

  # check-for-changes can be really slow.
  # You should disable it if you work with large repositories.
  zstyle ':vcs_info:*:prompt:*' check-for-changes false

  # Formats:
  # %b - branchname
  # %u - unstagedstr (see below)
  # %c - stagedstr (see below)
  # %a - action (e.g. rebase-i)
  # %R - repository path
  # %S - path in the repository
  local fmt_branch="(${__PROMPT_PAULMILLR_COLORS[1]}%b%f%u%c)"
  local fmt_action="(${__PROMPT_PAULMILLR_COLORS[5]}%a%f)"
  local fmt_unstaged="${__PROMPT_PAULMILLR_COLORS[2]}!%f"
  local fmt_staged="${__PROMPT_PAULMILLR_COLORS[5]}+%f"

  zstyle ':vcs_info:*:prompt:*' unstagedstr   "${fmt_unstaged}"
  zstyle ':vcs_info:*:prompt:*' stagedstr     "${fmt_staged}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${fmt_branch}${fmt_action}"
  zstyle ':vcs_info:*:prompt:*' formats       "${fmt_branch}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # ${__PROMPT_PAULMILLR_COLORS[3]}%n%f@${__PROMPT_PAULMILLR_COLORS[2]}%m%f in
  PROMPT="${__PROMPT_PAULMILLR_COLORS[4]}â†’ ${__PROMPT_PAULMILLR_COLORS[5]}%1~%f "'${vcs_info_msg_0_}'" $ "
}

prompt_paulmillr_setup "$@"

