#
# Exposes information about the Python environment via the $python_info
# associative array.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Patrick Bos <egpbos@gmail.com>
#

# function python-info {

local virtualenv_format
local virtualenv_formatted

# Clean up previous $python_info.
unset python_info
typeset -gA python_info

# Format virtualenv.
if [[ -n "$VIRTUAL_ENV" ]]; then
  zstyle -s ':prezto:module:python:info:virtualenv' format 'virtualenv_format'
  zformat -f virtualenv_formatted "$virtualenv_format" "v:${VIRTUAL_ENV:t}"
  python_info[virtualenv]="$virtualenv_formatted"
  PY_VERSION=`python -V 2>&1`
  python_info[version]="py-"$PY_VERSION[(w)2]
fi

# Do the same for Conda virtual environments
if [[ -n "$CONDA_DEFAULT_ENV" ]]; then
  zstyle -s ':prezto:module:python:info:virtualenv' format 'virtualenv_format'
  zformat -f virtualenv_formatted "$virtualenv_format" "v:${CONDA_DEFAULT_ENV:t}"
  python_info[virtualenv]="$virtualenv_formatted"
fi

# }
