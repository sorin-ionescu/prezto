#
# Exposes information about the Node.js environment via the $node_info
# associative array.
#
# Authors:
#   Zeh Rizzatti <zehrizzatti@gmail.com>
#

# function node-info {

local version
local version_format
local version_formatted

unset node_info
typeset -gA node_info

if (( $+functions[nvm_version] )); then
  version="${$(nvm_version)#v}"
elif (( $+commands[nodenv] )); then
  version="${${$(nodenv version)#v}[(w)0]}"
elif (( $+commands[node] )) ; then
  version="${$(node -v)#v}"  
fi

if [[ "$version" != (none|system) ]]; then
  zstyle -s ':prezto:module:node:info:version' format 'version_format'
  zformat -f version_formatted "$version_format" "v:$version"
  node_info[version]="$version_formatted"
fi

# }
