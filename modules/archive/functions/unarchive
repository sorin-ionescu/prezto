#
# Extracts the contents of archives.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# function unarchive {

local remove_archive
local success
local file_name
local file_path
local extract_dir
local _gzip_bin _bzip2_bin _xz_bin _zstd_bin

if (( $# == 0 )); then
  cat >&2 <<EOF
usage: $0 [-option] [file ...]

options:
    -r, --remove    remove archive

Report bugs to <sorin.ionescu@gmail.com>.
EOF
fi

remove_archive=1
if [[ "$1" == "-r" || "$1" == "--remove" ]]; then
  remove_archive=0
  shift
fi

# here, we check for dropin/multi-threaded replacements
# this should eventually be moved to modules/archive/init.zsh
# as a global alias
if (( $+commands[unpigz] )); then
  _gzip_bin='unpigz'
else
  _gzip_bin='gunzip'
fi

if (( $+commands[pixz] )); then
  _xz_bin='pixz -d'
else
  _xz_bin='xz'
fi

if (( $+commands[lbunzip2] )); then
  _bzip2_bin='lbunzip2'
elif (( $+commands[pbunzip2] )); then
  _bzip2_bin='pbunzip2'
else
  _bzip2_bin='bunzip2'
fi

_zstd_bin='zstd'

while (( $# > 0 )); do
  if [[ ! -s "$1" ]]; then
    print "$0: file not valid: $1" >&2
    shift
    continue
  fi

  success=0
  file_name="${1:t}"
  file_path="${1:A}"
  extract_dir="${file_name:r}"
  case "$1:l" in
    (*.tar.gz|*.tgz) tar -xvf "$1" --use-compress-program="${_gzip_bin}" ;;
    (*.tar.bz2|*.tbz|*.tbz2) tar -xvf "$1" --use-compress-program="${_bzip2_bin}" ;;
    (*.tar.xz|*.txz) tar -xvf "$1" --use-compress-program="${_xz_bin}" ;;
    (*.tar.zma|*.tlz) tar --lzma --help &> /dev/null \
      && tar --lzma -xvf "$1" \
      || lzcat "$1" | tar -xvf - ;;
    (*.tar.zst|*.tzst) tar -xvf "$1" --use-compress-program="${_zstd_bin}" ;;
    (*.tar) tar -xvf "$1" ;;
    (*.gz) gunzip "$1" ;;
    (*.bz2) bunzip2 "$1" ;;
    (*.xz) unxz "$1" ;;
    (*.lzma) unlzma "$1" ;;
    (*.Z) uncompress "$1" ;;
    (*.zip|*.jar) unzip "$1" -d $extract_dir ;;
    (*.rar) ( (( $+commands[unrar] )) \
      && unrar x -ad "$1" ) \
      || ( (( $+commands[rar] )) \
      && rar x -ad "$1" ) \
      || unar -d "$1" ;;
    (*.7z) 7za x "$1" ;;
    (*.deb)
      mkdir -p "$extract_dir/control"
      mkdir -p "$extract_dir/data"
      cd "$extract_dir"; ar vx "${file_path}" > /dev/null
      cd control; tar xvf ../control.tar.*
      cd ../data; tar xvf ../data.tar.*
      cd ..; rm control.tar.* data.tar.* debian-binary
      cd ..
    ;;
    (*)
      print "$0: cannot extract: $1" >&2
      success=1
    ;;
  esac

  (( success = $success > 0 ? $success : $? ))
  (( $success == 0 )) && (( $remove_archive == 0 )) && rm "$1"
  shift
done

# }
